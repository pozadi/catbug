<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8">
    <title>Catbug demo | ToDo</title>

    <style type="text/css">
      .todos-widget {
        width: 500px;
        margin: 0 auto;
        font-size: 20px;
        line-height: 1.5;
      }
        .item-list {
          list-style: none;
          margin: 0;
          padding: 0;
        }
          .todo-item,
          .add-form {
            background: #8ee699;
            padding: 4px 6px;
          }
            .todo-item [type=text],
            .add-form [type=text] {
              width: 90%;
              font-size: 20px;
              padding: 5px 7px;
            }
              .remove {
                float: right;
              }
              .todo-item [type=text] {
                display: none;
              }
              .todo-item.edit .remove,
              .todo-item.edit .text,
              .todo-item.edit [type=checkbox] {
                display: none;
              }
              .todo-item.edit [type=text] {
                display: inline;
              }
              .todo-item.completed .text {
                text-decoration: line-through;
              }
            .todos-widget.filter-completed .todo-item.active {
              display: none;
            }
            .todos-widget.filter-active .todo-item.completed {
              display: none;
            }
        .footer {
          background: #63c26f;
          margin: 0 4px;
          font-size: 14px;
          padding: 5px 7px;
        }
          .left {
            float: left;
          }
          .clear-completed {
            float: right;
          }
          .footer-menu {
            list-style: none;
            margin: 0;
            padding: 0;
            text-align: center;
          }
            .footer-menu li {
              display: inline;
            }
              .todos-widget.filter-all [data-filter=all] {
                font-weight: bold;
              }
              .todos-widget.filter-completed [data-filter=completed] {
                font-weight: bold;
              }
              .todos-widget.filter-active [data-filter=active] {
                font-weight: bold;
              }
    </style>
  </head>
  <body>

    <div class="js-todos-widget todos-widget">
      <h1>todos</h1>

      <div class="add-form">
        <input type="checkbox" class="js-all-checkbox">
        <input type="text" placeholder="What needs to be done?" class="js-new-item">
      </div>

      <ul class="js-item-list item-list">
      </ul>

      <div class="footer js-footer">
        <span class="js-left-counter left"></span>

        <button class="js-clear-completed clear-completed">Clear completed
          (<span class="js-completed-counter"></span>)</button>

        <ul class="footer-menu">
          <li><button class="js-set-filter-button" data-filter="all">All</button></li>
          <li><button class="js-set-filter-button" data-filter="active">Active</button></li>
          <li><button class="js-set-filter-button" data-filter="completed">Completed</button></li>
        </ul>
      </div>

      <script type="text/html" class="js-item-template">
        <li class="js-todo-item todo-item">
          <input type="checkbox" class="js-checkbox">
          <span class="js-text text"></span>
          <button class="js-remove remove">&times;</button>
          <input type="text" class="js-field">
        </li>
      </script>
    </div>

    <script src="../components/underscore/underscore.js"></script>
    <script src="../components/jquery/jquery.js"></script>
    <script src="../catbug.js"></script>

    <script src="../components/coffee-script/extras/coffee-script.js"></script>

    <script type="text/coffeescript">

      onEnter = (fn) ->
        (e) -> fn.apply(@, arguments) if e.which == 13

      catbug """
        .js-todo-item
          .js-checkbox
          .js-text
          .js-remove
          .js-field
      """, "todo-item", (ns) ->

        completedCb = $.Callbacks()
        removedCb = $.Callbacks()

        api =

          setText: (text) ->
            ns.el.text.text text
            ns.el.field.val text

          getText: ->
            ns.el.text.text()

          setCompleted: (completed) ->
            ns.root.removeClass('active completed')
            ns.root.addClass(if completed then 'completed' else 'active')
            ns.el.checkbox.prop 'checked', completed
            completedCb.fire completed

          getCompleted: ->
            ns.el.checkbox.prop 'checked'

          remove: ->
            ns.root.remove()
            removedCb.fire()

          onCompleted: completedCb.add
          onRemoved: removedCb.add

        ns.el.checkbox.change ->
          api.setCompleted ns.el.checkbox.prop 'checked'

        ns.el.text.dblclick ->
          ns.root.addClass('edit')
          ns.el.field.focus()

        __save = ->
          ns.root.removeClass('edit')
          api.setText ns.el.field.val()

        ns.el.field.blur __save
        ns.el.field.keyup onEnter __save

        ns.el.remove.click ->
          api.remove()

        api


      catbug """
        .js-todos-widget
          .js-all-checkbox
          .js-new-item
          .js-item-list
            .js-todo-item
          .js-footer
            .js-left-counter
            .js-clear-completed
              .js-completed-counter
            .js-set-filter-button
          .js-item-template
      """, ->

        itemTpl = $.trim @el.itemTemplate.html()

        addItem = (text) =>
          el = $ itemTpl
          @el.itemList.append el
          @el.todoItem.update()
          item = el.catbug 'todo-item'
          item.setText text
          item.setCompleted false
          item.onRemoved =>
            @el.todoItem.update()
            updateStats()
          item.onCompleted updateStats
          updateStats()

        setFilter = (filter) =>
          @root.removeClass('filter-completed filter-active filter-all')
          @root.addClass("filter-#{filter}")

        items = (onlyCompleted=false) =>
          itemAPIs = @el.todoItem.map -> $(this).catbug('todo-item')
          if onlyCompleted
            _.filter itemAPIs, (api) -> api.getCompleted()
          else
            itemAPIs

        updateStats = =>
          all = items().length
          completed = items(true).length
          active = all - completed

          @el.footer.toggle(all > 0)
          @el.clearCompleted.toggle(completed > 0)
          @el.completedCounter.text(completed)
          @el.leftCounter.text("#{active} #{if active == 1 then 'item' else 'items'} left")

        @el.newItem.keyup onEnter =>
          addItem @el.newItem.val()
          @el.newItem.val('')

        @el.allCheckbox.change =>
          completed = @el.allCheckbox.prop('checked')
          item.setCompleted(completed) for item in items()

        @el.clearCompleted.click =>
          item.remove() for item in items(true)

        @el.setFilterButton.click (ev) =>
          setFilter $(ev.target).data('filter')

        setFilter('all')
        updateStats()

    </script>
  </body>
</html>

